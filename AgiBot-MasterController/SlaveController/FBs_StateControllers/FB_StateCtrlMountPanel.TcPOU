<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_StateCtrlMountPanel" Id="{823d9da4-dfa7-45fa-b541-870812882ed0}" SpecialFunc="None">
    <Declaration><![CDATA[// Brief: Master Arm Standby
FUNCTION_BLOCK PUBLIC FB_StateCtrlMountPanel EXTENDS FB_SlaveArmCtrlBase
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	// mount status
	m_mountStatus :INT := 0;
	
	// flag if joint is mounted
	m_jointMounted :ARRAY[1..5] OF BOOL := [5(FALSE)];
	
	// mount motion planning
	m_jntOTG :ARRAY[1..5] OF FB_secOrdTrajFilter;
	m_panelMountVel :LREAL := 30* g_deg2Rad;
	m_panelMountAcc :LREAL := PI;
	
	// track error threshold which indicate motor is buckled with panel
	m_maxTrackErr :LREAL := 5*g_deg2Rad;
	
	// command mount motion
	m_mountStepPos :LREAL := 30.0 * g_deg2Rad;
	m_targetMountPos :Vec5d;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="calcCmdJntPos" Id="{19d93df1-8900-4356-b776-97d66cc8cc37}">
      <Declaration><![CDATA[// override this function in each exact controller
METHOD PROTECTED calcCmdJntPos : BOOL
VAR_INPUT
	motionScale :LREAL;
	masterIdx :INT;
END_VAR

VAR_IN_OUT CONSTANT
	i_slaveArm :FB_SlaveArm;
	i_masterStatus :ST_MasterStatus;
END_VAR

VAR 
	i :INT;
	curJntVel :Vec8d;
	curJntPos :Vec8d;
	jntPosErr :Vec8d;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
curJntPos := i_slaveArm.curJntPos;

CASE m_mountStatus OF
	// To lock joint with panel
	0:
		FOR i:=1 TO 5 DO
			// check joint buckled with panel
			IF ABS(m_cmdJntPos[i+3]-curJntPos[i+3])> m_maxTrackErr THEN 
				m_jointMounted[i]:=TRUE;
				m_targetMountPos[i]:=curJntPos[i+3];
			END_IF
			
			// set command joint position step
			IF FALSE = m_jointMounted[i] THEN
				m_targetMountPos[i]:=curJntPos[i+3]+m_mountStepPos;
			END_IF
		END_FOR
		
		IF (m_jointMounted[1] AND m_jointMounted[2] AND m_jointMounted[3] AND m_jointMounted[4] AND m_jointMounted[5]) THEN
			m_mountStatus:=1;
		END_IF
	
	// determine zero position
	1:
		FOR i:=1 TO 5 DO
			m_targetMountPos[i]:=curJntPos[i+3]-g_panelLockedPosition[i];
		END_FOR
		m_mountStatus :=2;
	
	// move back to zero position
	2:
		vecSub(m_targetMountPos,curJntPos,jntPosErr);
		IF vecNorm(jntPosErr)<GVL_SlaveControlParameters.g_jntCtrlPosTol THEN
			m_isFinished:=TRUE;
		END_IF
END_CASE

// traj plan
FOR i:=1 TO 5 DO
	m_jntOTG[i].run(m_targetMountPos[i],0.0, o_cmdAcc=>m_cmdJntAcc[3+i],o_cmdVel=>m_cmdJntVel[3+i],o_cmdPos=>m_cmdJntPos[i+3]);
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="init" Id="{ee3f7839-f2f4-4010-851e-5bc229c0ad27}">
      <Declaration><![CDATA[METHOD PUBLIC init : BOOL
VAR_IN_OUT CONSTANT
	i_slaveArm	:FB_SlaveArm;
END_VAR
VAR_IN_OUT 
	r_slaveArmCtrlCmd :ST_SlaveArmCtrlCmds;
END_VAR
VAR
	i : INT;
END_VAR

]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Init(i_slaveArm,r_slaveArmCtrlCmd);

m_mountStatus:=0;
FOR i:=1 TO 5 DO
	m_targetMountPos[i]:=m_cmdJntPos[i+3];
	m_jntOTG[i].init(m_targetMountPos[i], 0,m_panelMountVel, m_panelMountAcc, g_slaveArmCtrlCycleTime);
	m_jointMounted[i]:=FALSE;
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_StateCtrlMountPanel">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_StateCtrlMountPanel.calcCmdJntPos">
      <LineId Id="149" Count="0" />
      <LineId Id="120" Count="0" />
      <LineId Id="154" Count="0" />
      <LineId Id="144" Count="0" />
      <LineId Id="155" Count="0" />
      <LineId Id="157" Count="0" />
      <LineId Id="160" Count="0" />
      <LineId Id="208" Count="0" />
      <LineId Id="161" Count="1" />
      <LineId Id="209" Count="0" />
      <LineId Id="163" Count="1" />
      <LineId Id="210" Count="1" />
      <LineId Id="170" Count="1" />
      <LineId Id="158" Count="0" />
      <LineId Id="172" Count="14" />
      <LineId Id="188" Count="2" />
      <LineId Id="156" Count="0" />
      <LineId Id="118" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="146" Count="2" />
      <LineId Id="150" Count="0" />
    </LineIds>
    <LineIds Name="FB_StateCtrlMountPanel.init">
      <LineId Id="57" Count="1" />
      <LineId Id="69" Count="0" />
      <LineId Id="68" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="71" Count="0" />
      <LineId Id="12" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>