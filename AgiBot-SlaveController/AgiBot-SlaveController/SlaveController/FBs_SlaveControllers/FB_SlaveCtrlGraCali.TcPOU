<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_SlaveCtrlGraCali" Id="{9ec40fb0-5513-4ed9-babf-e07e71772a70}" SpecialFunc="None">
    <Declaration><![CDATA[// Brief: Slave Arm Gravity Calibration
FUNCTION_BLOCK PUBLIC FB_SlaveCtrlGraCali EXTENDS FB_SlaveArmCtrlBase
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
// cali trajectory
	m_caliMotionSegs :INT :=3;
	m_graCaliTraj:ARRAY [1..3, 1..g_sJntNum] OF LREAL:=
		[-pi/6, -PI/2+pi/6, 0.5, 	5(0),
		 pi/6, 	-PI/2-pi/4, 0.3, 	5(0),
		 -pi/6, -PI/2+pi/6, 0.5, 	5(0)];
		 
	m_caliMaxjntVel:Vec8d:=[2(2.5*g_deg2Rad), 0.01, 5(2.5*g_deg2Rad)];
	m_caliMaxjntAcc:Vec8d:=[8(0.5)];
	m_caliMaxjntJerk:Vec8d:=[8(2.0)];
	
	m_caliTrajIdx: INT := 1;
	m_caliPlanner: ARRAY[1..3] OF FB_TrajMoveJ;
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="calcCmdJntPos" Id="{e8388ef5-ff5f-4bc0-805c-b4f37a5ed6ad}">
      <Declaration><![CDATA[// override this function in each exact controller
METHOD PROTECTED calcCmdJntPos : BOOL
VAR_IN_OUT CONSTANT
	i_endoscopePose : ST_Frame;
	i_masterStatus :ST_MasterStatus;
	i_setupStatus: ST_SetupStatus;
	i_slaveArm :FB_SlaveArm;
END_VAR


VAR 
	i :INT;
	curJntVel :Vec8d;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// check if planner is ready
IF m_caliPlanner[m_caliTrajIdx].isGene = FALSE THEN
	calcCmdJntPos:=FALSE;
	RETURN;
END_IF

// traj plan
calcCmdJntPos :=m_caliPlanner[m_caliTrajIdx].evaluate(m_timePeriod, jntPos=> m_cmdJntPos, jntVel=>m_cmdJntVel, jntAcc=> m_cmdJntAcc);

// check if to transit to next segment
curJntVel :=i_slaveArm.curJntVel;
IF(m_timePeriod > m_caliPlanner[m_caliTrajIdx].duration  AND norm(curJntVel)<7*GVL_SlaveControlParameters.g_jntStaticVelThres) THEN
	m_caliTrajIdx:=m_caliTrajIdx+1;
	m_timePeriod:=0;
END_IF

// check if to terminated
IF(m_caliTrajIdx > m_caliMotionSegs) THEN
	m_isFinished:=TRUE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="init" Id="{1fed4a27-3de6-4ef1-8a15-2cb2fd5aafb3}">
      <Declaration><![CDATA[METHOD PUBLIC init : BOOL
VAR_IN_OUT CONSTANT
	i_slaveArm	:FB_SlaveArm;
END_VAR
VAR_IN_OUT 
	r_slaveArmCtrlCmd :ST_SlaveArmCtrlCmds;
END_VAR

VAR
	i,j :INT;
	initJntPos,startJntPos,endJntPos :Vec8d;

END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Init(i_slaveArm,r_slaveArmCtrlCmd);
m_caliTrajIdx :=1;
m_jntOPMode := g_slaveAllPosTrqMode;

// traj plan
// fisrt segment
initJntPos:=i_slaveArm.curJntPos;
startJntPos:=initJntPos;
endJntPos:=startJntPos;
FOR j:=1 TO 3 DO 
	endJntPos[j]:=m_graCaliTraj[1,j];
END_FOR
m_caliPlanner[1].init(startJntPos,endJntPos,m_caliMaxjntVel, m_caliMaxJntAcc, m_caliMaxJntJerk);

// other segments
FOR i:=2 TO m_caliMotionSegs DO 
	startJntPos := endJntPos;
	endJntPos:=startJntPos;
	FOR j:=1 TO 3 DO 
		endJntPos[j]:=m_graCaliTraj[i,j];
	END_FOR
	m_caliPlanner[i].init(startJntPos,endJntPos,m_caliMaxjntVel, m_caliMaxJntAcc, m_caliMaxJntJerk);
END_FOR

// update commands to arm
copyCmds(r_slaveArmCtrlCmd);
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_SlaveCtrlGraCali">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_SlaveCtrlGraCali.calcCmdJntPos">
      <LineId Id="114" Count="18" />
      <LineId Id="98" Count="0" />
    </LineIds>
    <LineIds Name="FB_SlaveCtrlGraCali.init">
      <LineId Id="57" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="121" Count="0" />
      <LineId Id="192" Count="0" />
      <LineId Id="70" Count="0" />
      <LineId Id="138" Count="0" />
      <LineId Id="179" Count="0" />
      <LineId Id="82" Count="0" />
      <LineId Id="165" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="183" Count="0" />
      <LineId Id="99" Count="0" />
      <LineId Id="74" Count="0" />
      <LineId Id="125" Count="0" />
      <LineId Id="139" Count="0" />
      <LineId Id="127" Count="0" />
      <LineId Id="189" Count="0" />
      <LineId Id="222" Count="0" />
      <LineId Id="135" Count="0" />
      <LineId Id="190" Count="0" />
      <LineId Id="132" Count="0" />
      <LineId Id="128" Count="0" />
      <LineId Id="126" Count="0" />
      <LineId Id="207" Count="1" />
      <LineId Id="206" Count="0" />
      <LineId Id="152" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>