<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_CartCtrlAsepticDeploy" Id="{2938379a-3fa1-4919-bcaa-4be4c07fb559}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_CartCtrlAsepticDeploy EXTENDS FB_CartCtrlBase
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR

	m_actJntPos: Vec8d;
	m_tarJntPos: Vec8d;
	m_jntPosArrival : Vec8b;
	m_initActJntPos: Vec8d;
	m_initTarJntPos : Vec8d;
	m_incTarJntPos : Vec8d;

	// time threshold to 5-8joint after it's enabled
	m_cartAdjustStartWaitTime :LREAL := 0.3;
	
	//adjust start  time 
	m_adjustStartTime :LREAL;
	
	m_jntOTG :ARRAY[1..8] OF FB_secOrdTrajFilter;
	//m_trajPlan :ARRAY[1..8] OF TrapezoIDal_velocity_trajectory;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
]]></ST>
    </Implementation>
    <Method Name="calcCmdJntPos" Id="{fd46bf18-fc5e-4487-a850-60333e5fde9d}">
      <Declaration><![CDATA[// override this function in each exact controller
METHOD PROTECTED calcCmdJntPos : BOOL
VAR_IN_OUT CONSTANT
	// slave cart joint data
	i_slaveCart :FB_SlaveCart;
END_VAR

VAR
	i:INT;
	actPosErr : vec8d;
	tarPosErr : vec8d;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[//In this method, 5 represents the pillar postion , 6 represents the beam rotation angle ,7 represents the beam stretch position ,8 represents the overhang angle
m_adjustStartTime :=LIMIT(0, m_adjustStartTime + g_cartCtrlCycleTime, 1);
m_actJntPos := i_slaveCart.curJntPos;
IF i_slaveCart.ctrlEnable.m_dockingEnable THEN
	m_initTarJntPos := i_slaveCart.m_cartDataFromUI.m_cartSpecificPosDocking;
ELSIF i_slaveCart.ctrlEnable.m_drapingEnable THEN
	m_initTarJntPos := i_slaveCart.m_cartDataFromUI.m_cartSpecificPosDraping;
END_IF
vecSub(m_initTarJntPos, m_actJntPos, actPosErr);
vecSub(m_initTarJntPos, m_tarJntPos, tarPosErr);

IF m_adjustStartTime >= m_cartAdjustStartWaitTime THEN
	FOR i := 5 TO 8 BY 1 DO
		IF ABS(actPosErr[i]) <= GVL_CartControlParameters.g_jntCtrlAngTolHigh*2  THEN
			m_jntPosArrival[i] := TRUE;
		END_IF
		
		IF ABS(tarPosErr[i]) > GVL_CartControlParameters.g_jntCtrlAngTolHigh THEN
			m_incTarJntPos[i] := m_incTarJntPos[i] + sign(actPosErr[i]) * GVL_CartControlParameters.g_maxJntVel[i]/2 * g_cartCtrlCycleTime;
			m_tarJntPos[i] := m_actJntPos[i] + m_incTarJntPos[i];
		END_IF
	END_FOR
	
	IF m_jntPosArrival[5] AND m_jntPosArrival[6] AND m_jntPosArrival[7] AND m_jntPosArrival[8] THEN
		m_isFinished := TRUE;
	END_IF
	
	FOR i := 5 TO 8 BY 1 DO
		m_jntOTG[i].run(i_targetPos:= m_tarJntPos[i], i_targetVel:= 0.0, o_cmdAcc=> , o_cmdVel=> , o_cmdPos=>m_cmdJntPos[i] );
	END_FOR
END_IF

]]></ST>
      </Implementation>
    </Method>
    <Method Name="init" Id="{94ed4de9-81b7-45eb-b719-e1d58a1c398a}">
      <Declaration><![CDATA[// override this function in each exact controller
// NOTICE: set joint control mode here, and do some initialization if needed
METHOD PUBLIC init : BOOL
VAR_IN_OUT CONSTANT
	i_slaveCart	:FB_SlaveCart;
END_VAR
VAR_IN_OUT 
	r_cartCtrlCmd :ST_SlaveArmCtrlCmds;
END_VAR
VAR
	i : INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[m_holdLastCmds := checkToHoldLastCmds(i_slaveCart);
SUPER^.init(i_slaveCart,r_cartCtrlCmd);

m_jntEnableFlag[5] :=1;
m_jntEnableFlag[6] :=1;
m_jntEnableFlag[7] :=1;
m_jntEnableFlag[8] :=1;

m_jntOPMode[5]:= DriverOPMode_cspMZ;
m_jntOPMode[6]:= DriverOPMode_Pos;
m_jntOPMode[7]:= DriverOPMode_Pos;
m_jntOPMode[8]:= DriverOPMode_Pos;

m_initActJntPos:=m_cmdJntPos;//i_slaveCart.curJntPos;
m_jntPosArrival := g_zeroVec8b;
m_incTarJntPos := g_zeroVec8d;
m_tarJntPos := g_zeroVec8d;
m_jntPosArrival := g_zeroVec8b;

IF i_slaveCart.ctrlEnable.m_dockingEnable THEN
	m_initTarJntPos := i_slaveCart.m_cartDataFromUI.m_cartSpecificPosDocking;
ELSIF i_slaveCart.ctrlEnable.m_drapingEnable THEN
	m_initTarJntPos := i_slaveCart.m_cartDataFromUI.m_cartSpecificPosDraping;
END_IF

FOR i := 1 TO 4 BY 1 DO
	m_jntOTG[i+4].init(i_curCmdPos:= m_cmdJntPos[i+4], i_curCmdVel:= 0, i_maxV:=GVL_CartControlParameters.g_maxJntVel[i+4] , i_maxA:=GVL_CartControlParameters.g_maxJntAcc[i+4] , i_Ts:=g_cartCtrlCycleTime );
END_FOR
//m_initTarJntPos := m_tarJntPos;
m_adjustStartTime := 0;
// update commands to arm
copyCmds(r_cartCtrlCmd);]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_CartCtrlAsepticDeploy">
      <LineId Id="52" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_CartCtrlAsepticDeploy.calcCmdJntPos">
      <LineId Id="167" Count="31" />
      <LineId Id="14" Count="0" />
    </LineIds>
    <LineIds Name="FB_CartCtrlAsepticDeploy.init">
      <LineId Id="136" Count="30" />
      <LineId Id="21" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>