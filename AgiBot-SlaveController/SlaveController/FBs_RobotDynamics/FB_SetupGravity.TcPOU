<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_SetupGravity" Id="{5452bfb6-8061-49fa-846a-f3728f3e7824}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_SetupGravity
VAR_INPUT
END_VAR
VAR
	m_armIdx :INT ;

	// current arm parameters
	m_armParams :ARRAY[1..7] OF LREAL;

	// gravity regression matrix
	regMat:ARRAY[1..7] OF LREAL;
	
	m_graivty :LREAL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
]]></ST>
    </Implementation>
    <Method Name="getRegMat" Id="{7045fe73-d3f1-4303-94f0-43372abb4236}">
      <Declaration><![CDATA[METHOD getRegMat : BOOL
VAR_INPUT
	// joint position of setup joint, joint6-8
	setupAng :LREAL;
	q1: LREAL;
	q2: LREAL;
	q3: LREAL;
END_VAR

VAR
	s0,c0,s1,c1,s2, c2,d3 :LREAL;
END_VAR

VAR_OUTPUT
	o_regMat:ARRAY[1..7] OF LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[s0:= SIN(setUpAng);
c0:= COS(setUpAng);
s1:=SIN(q1);
c1:=COS(q1);
s2:=SIN(q2);
c2:=COS(q2);
d3:=q3;

// setup joint
o_regMat[1] :=c0;
o_regMat[2] := s0;
o_regMat[3] :=-(c2*s0*d3+c0*c1*s2*d3);
o_regMat[4] :=  -c0*c1;
o_regMat[5] :=  -c0*s1;
o_regMat[6] := - (c0*c1*s2+c2*s0);
o_regMat[7] :=  s0*s2- c0*c1*c2  ;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="init" Id="{50ab6c0f-ea77-4dcf-ba79-983ca62b783b}">
      <Declaration><![CDATA[METHOD init : BOOL
VAR_INPUT
	i_armIdx :INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[m_armIdx :=i_armIdx;]]></ST>
      </Implementation>
    </Method>
    <Method Name="run" Id="{6d0c1aa2-0848-447c-a248-94105516dfb5}">
      <Declaration><![CDATA[METHOD run : BOOL
VAR_IN_OUT CONSTANT
	// setup joint angle
	i_setupJntPos :Vec5d;
	
	// slave joint angle
	i_slaveJntPos :Vec8d;
	
	// instrument
	i_instruStatus :ST_InstruStatus;
END_VAR
VAR_OUTPUT
	// setup joint3 gravity
	o_jnt3Gravity :LREAL;
	
	// setup joint5 gravity
	o_jnt5Gravity :LREAL;
END_VAR
VAR
	setupPos, q1, q2, q3 :LREAL;
	i,j:INT;
	
	gravityTrq :Vec4d;

END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[setupPos:=i_setupJntPos[5];
q1 := i_slaveJntPos[1];
q2 := i_slaveJntPos[2];
q3 := i_slaveJntPos[3];

// get regression matrx
getRegMat(setupPos, q1, q2, q3, o_regMat=>regMat);

// determine current arm parameters
m_armParams[1] := GVL_SetupArmDynamics.g_setupJnt5GravityParam[m_armIdx,1];
m_armParams[2] := GVL_SetupArmDynamics.g_setupJnt5GravityParam[m_armIdx,2];
IF FALSE = i_instruStatus.m_ready THEN
	// arm only
	FOR i:=1 TO 5 DO
		m_armParams[i+2]:= GVL_SlaveArmDynamics.g_gravityParam[m_armIdx,i];
	END_FOR

// endoscope
ELSIF InstruType_endoscope = i_instruStatus.m_type THEN 
	FOR i:=1 TO 5 DO
		m_armParams[i+2]:=GVL_SlaveArmDynamics.g_gravityParam[m_armIdx,i]+GVL_SlaveArmDynamics.g_gravityEndoscope[i];
	END_FOR

// instrument
ELSE
	FOR i:=1 TO 5 DO
		m_armParams[i+2]:=GVL_SlaveArmDynamics.g_gravityParam[m_armIdx,i]+GVL_SlaveArmDynamics.g_gravityInstrument[i];
	END_FOR
// TODO:Trocar
END_IF

// calculate gravity torque
o_jnt5Gravity:=0;
FOR j:=1 TO 7 DO
	o_jnt5Gravity:=o_jnt5Gravity+regMat[j]*m_armParams[j];
END_FOR
m_graivty := o_jnt5Gravity;

o_jnt3Gravity:= setupJnt3Gravity(i_setupJntPos[3]);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="setupJnt3Gravity" Id="{14b87259-982c-438c-9682-4b69bb2110c7}">
      <Declaration><![CDATA[METHOD setupJnt3Gravity : LREAL
VAR_INPUT
	i_jnt3Pos :LREAL;
END_VAR
VAR
	q:LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[q := LIMIT(GVL_SetupArmDynamics.g_setupJnt3GravityPosRange[m_armIdx,1], i_jnt3Pos, GVL_SetupArmDynamics.g_setupJnt3GravityPosRange[m_armIdx,2]);
setupJnt3Gravity:=GVL_SetupArmDynamics.g_setupJnt3GravityParams[m_armIdx,1]*EXPT(q,3)+GVL_SetupArmDynamics.g_setupJnt3GravityParams[m_armIdx,2]*EXPT(q,2)+
				GVL_SetupArmDynamics.g_setupJnt3GravityParams[m_armIdx,3]*q+GVL_SetupArmDynamics.g_setupJnt3GravityParams[m_armIdx,4];]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_SetupGravity">
      <LineId Id="51" Count="0" />
      <LineId Id="117" Count="0" />
    </LineIds>
    <LineIds Name="FB_SetupGravity.getRegMat">
      <LineId Id="197" Count="1" />
      <LineId Id="83" Count="0" />
      <LineId Id="153" Count="3" />
      <LineId Id="199" Count="0" />
      <LineId Id="152" Count="0" />
      <LineId Id="201" Count="0" />
      <LineId Id="244" Count="0" />
      <LineId Id="202" Count="1" />
      <LineId Id="200" Count="0" />
      <LineId Id="207" Count="1" />
      <LineId Id="176" Count="0" />
    </LineIds>
    <LineIds Name="FB_SetupGravity.init">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_SetupGravity.run">
      <LineId Id="137" Count="3" />
      <LineId Id="142" Count="3" />
      <LineId Id="182" Count="0" />
      <LineId Id="201" Count="1" />
      <LineId Id="183" Count="2" />
      <LineId Id="259" Count="0" />
      <LineId Id="187" Count="13" />
      <LineId Id="165" Count="2" />
      <LineId Id="203" Count="0" />
      <LineId Id="170" Count="2" />
      <LineId Id="234" Count="0" />
      <LineId Id="208" Count="0" />
      <LineId Id="207" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_SetupGravity.setupJnt3Gravity">
      <LineId Id="5" Count="1" />
      <LineId Id="14" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>