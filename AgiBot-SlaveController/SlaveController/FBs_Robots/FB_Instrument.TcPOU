<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_Instrument" Id="{8c0067f5-f42b-4462-b105-279d3ba7139d}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Instrument

VAR_IN_OUT CONSTANT
	i_slaveArm	:FB_SlaveArm;
END_VAR

VAR
	// instrument type
	m_instruType : 	E_InstrumentType := InstruType_UNKNOWM;
	
	// flag if instrument is singer finger
	m_isSingleFinger:BOOL:= FALSE;
	
	// current and max allowed read tries
	m_curReadTries :INT :=0;
	m_maxReadTries :INT := 5;
	
	//IP of operating arm
	m_armNetId:T_AmsNetId; 	
	
	//Joint port number
	m_jointInstruAddr:UINT;	

	//Arm LED Prot	
	m_LEDPortAddr:UINT;		
	
	//The state and parameters readed from the instrument
	m_onChipData:ST_InstruOnChipData;
	
	// instrument kinematic parameters
	m_kinematicParameters:ST_InstruKinematicParam; 
	
	// joint position when instrument is at ZERO posture
	m_jntPosOffset:Vec8d;
	
	// reduction from motor joint to endoscope rotate joint
	m_endoscopeRotateReduction :LREAL := 2.0/3.0;
	
	//endoscope type
	m_curEndoscopeType :E_EndoscopeType:=EndoScopeType_30;
	m_nextEndoscopeType :E_EndoscopeType:=EndoScopeType_30;
	
	//endoscope posutre
	m_curEndoscopePosture :E_EndoscopePosture:=EndoscopePosture_HeadDown;
	m_nextEndoscopePosture :E_EndoscopePosture:=EndoscopePosture_HeadDown;
	
	// udint code for zero position
	m_zeroPosCode : UDINT;
	
	// joint4 zero position, which is supposed to be a contant value
	m_jnt4ZeroPos :LREAL := 5.7;
	
	// variables used to read instrument information
	m_Step:INT;
	m_Value:UDINT;
	m_ReadDelay:TON;
	m_DealyTime:TIME:=T#0.5S;
	m_IDReadResult:UDINT;
	m_IDResult:ULINT;
	m_SeriesReadResult:UDINT;
	m_PosReadResult:UDINT;
	m_PosL1,m_PosL2,m_PosL3:UDINT;
	m_signL1,m_signL2,m_signL3:UDINT;
	m_UsageReadResult:ULINT;
	m_InstruUsageToLED:ULINT;
	m_InstruSeriesRecord:ARRAY [1..100] OF ULINT;
	m_HaveRecord:BOOL;
	m_SdoWrite:FB_EcCoESdoWrite;
	m_SdoRead:FB_EcCoESdoRead;
	
END_VAR

]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="property" Id="{9d49480e-8469-42fe-9794-09bbda49b553}" />
    <Method Name="checkInstruType" Id="{97020e4c-24ed-4bb8-b0ec-bca5a733a6da}">
      <Declaration><![CDATA[METHOD PROTECTED checkInstruType : BOOL
VAR_INPUT
END_VAR
VAR
	paramIdx :INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[checkInstruType := m_onChipData.m_InstruID >= InstruType_First AND m_onChipData.m_InstruID <= InstruType_Last ;

// return directly if instrument ID is invalid
IF NOT(checkInstruType) THEN
	RETURN;
END_IF
m_onChipData.m_Instrutype :=  m_onChipData.m_InstruID;
m_instruType := m_onChipData.m_InstruID;
m_isSingleFinger :=  (m_instruType = InstruType_CauteryHook OR  m_instruType = InstruType_CauterySpatula );

// update instrument kinematic parameters based on readed info
// TODO: move instrument kinematic parameters to chip 
paramIdx :=LIMIT(1, m_onChipData.m_InstruID, 30);
m_kinematicParameters.k13:=k13[paramIdx];
m_kinematicParameters.k23:=k23[paramIdx];
m_kinematicParameters.kx:=kx[paramIdx];
m_kinematicParameters.ky:=ky[paramIdx];
m_kinematicParameters.kz:=kz[paramIdx];
		
// rod length
IF InstruType_Endoscope =  m_instruType THEN
	m_kinematicParameters.m_rodLength := 0.48;
ELSE 
	m_kinematicParameters.m_fingerLength := 0.5392;
END_IF

// head length
IF InstruType_Endoscope =  m_instruType THEN
	m_kinematicParameters.m_headLength := 0.0;
ELSE 
	m_kinematicParameters.m_fingerLength := 0.01;
END_IF

// finger length
IF InstruType_NeedleDriver =  m_instruType THEN
	m_kinematicParameters.m_fingerLength := 0.01;
ELSE 
	m_kinematicParameters.m_fingerLength := 0.0;
END_IF

//TODO: update instrument joint motion range
IF m_isSingleFinger THEN
	m_kinematicParameters.m_wristMinDHJntPos[3]:=-110.0 * g_deg2Rad;
	m_kinematicParameters.m_wristMaxDHJntPos[3]:=110.0 * g_deg2Rad;
ELSE
	m_kinematicParameters.m_wristMinDHJntPos[3]:=-90.0 * g_deg2Rad;
	m_kinematicParameters.m_wristMaxDHJntPos[3]:=90.0 * g_deg2Rad;
END_IF


]]></ST>
      </Implementation>
    </Method>
    <Property Name="curScopePosture" Id="{c327ac7d-1ff6-4f6a-8d7d-377863afa9ff}" FolderPath="property\">
      <Declaration><![CDATA[PROPERTY curScopePosture : E_EndoscopePosture]]></Declaration>
      <Get Name="Get" Id="{3340ac94-9c36-4f32-8601-dac4725b2a6d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[curScopePosture:= m_curEndoscopePosture;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{f31b6899-d58a-4db8-8485-bf28ba7830a5}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[m_curEndoscopePosture := curScopePosture;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="curScopeType" Id="{4bce8a98-2eb7-4bb1-9eae-0e7d18b990ab}" FolderPath="property\">
      <Declaration><![CDATA[PROPERTY curScopeType : E_EndoscopeType]]></Declaration>
      <Get Name="Get" Id="{abceb35e-0bca-4bf2-b494-bd59bc26f3c4}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[curScopeType := m_curEndoscopeType;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{b042366b-dcc9-4e34-ab02-a26f6cf69d06}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[m_curEndoscopeType := curScopeType;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="gripAngleMap" Id="{b8d7e03a-0cbe-42c6-a7e6-6b03109deb8f}">
      <Declaration><![CDATA[METHOD PUBLIC gripAngleMap : BOOL
VAR_INPUT
	// master gripper angle
	i_masterGripAng :LREAL;
END_VAR

VAR_OUTPUT
	// slave instrument gripper angle
	o_instruGripAngle :LREAL;
	
	// slave instrument grip force	
	o_instruGripForceLevel :LREAL;
END_VAR

VAR
	gripLevel :REAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// suppose finger is  closed and zero force if instrument has only one finger
IF m_isSingleFinger THEN
	o_instruGripAngle := 0;
	o_instruGripForceLevel:= 0;
	RETURN;
END_IF 

// finger closed and force applied
IF i_masterGripAng < g_masterGripForceStartAng THEN
	o_instruGripAngle := m_kinematicParameters.m_minGripAngle;
	// NOTE: force is applied from 50%，as minimum gripper angle already apply some sort of force
	o_instruGripForceLevel := 50+50*(g_masterGripForceStartAng - i_masterGripAng)/(g_masterGripForceStartAng - g_minMasterGripAngle);
// finger open
ELSE
	o_instruGripAngle := (g_maxMasterGripAngle - i_masterGripAng)/(g_maxMasterGripAngle - g_masterGripForceStartAng);
	o_instruGripAngle :=m_kinematicParameters.m_maxGripAngle - o_instruGripAngle * (m_kinematicParameters.m_maxGripAngle - m_kinematicParameters.m_minGripAngle);
	o_instruGripForceLevel:=0;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="gripperAngleForwardTransform_ID12" Id="{d3dda96d-7b55-4cf3-9ffe-c8cf53ace77d}">
      <Declaration><![CDATA[METHOD PROTECTED gripperAngleForwardTransform_ID12 : BOOL
VAR_INPUT
	frontAngle:LREAL;
END_VAR
VAR_OUTPUT
	endAngle:LREAL;
	Succeed:BOOL;
END_VAR
VAR
	minFrontAng :LREAL  := 16.6*2*g_deg2Rad;
	maxFrontAng :LREAL := 49.18*2*g_deg2Rad;
	AB :LREAL := 2.8;
	BC:LREAL := 5.26;
	Angle_Initial_A:LREAL := 16.6 * g_deg2Rad; 
	SIN_C,Angle_Initial_C,End_Angle_Single,Angle_Inc_C,Angle_C,SIN_A,Angle_A,Angle_Inc_A:LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
//**************已知：器械前端开合角，求解：末端开合角***************//

//输入合法性判断，物理极限是[16.6*2，49.18*2]度
frontAngle := LIMIT(minFrontAng, frontAngle, maxFrontAng);


SIN_C:=AB*SIN(Angle_Initial_A)/BC;
Angle_Initial_C:=ASIN(SIN_C);

Angle_Inc_A:=frontAngle/2;
Angle_A:=Angle_Initial_A+Angle_Inc_A;

SIN_C:=AB*SIN(Angle_A)/BC;
Angle_C:=ASIN(SIN_C);

Angle_Inc_C:=Angle_C-Angle_Initial_C;
endAngle:=Angle_Inc_C*2;


]]></ST>
      </Implementation>
    </Method>
    <Method Name="gripperAngleInverseTransform_ID12" Id="{d41bf91a-0d5c-464e-b520-ee71cb3a1637}">
      <Declaration><![CDATA[METHOD PROTECTED gripperAngleInverseTransform_ID12 : BOOL
VAR_INPUT
	endAngle:LREAL;
END_VAR
VAR_OUTPUT
	frontAngle:LREAL;
	Succeed:BOOL;
END_VAR
VAR
	minEndAng :LREAL  := 0;
	maxEndAng :LREAL := 38*g_deg2Rad;
	AB :LREAL := 2.8;
	BC:LREAL := 5.26;
	Angle_Initial_A:LREAL := 16.6 * g_deg2Rad; 
	
	SIN_C,Angle_Initial_C,End_Angle_Single,Angle_Inc_C,Angle_C,SIN_A,Angle_A,Angle_Inc_A:LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
//***************已知：器械末端开合角，求解：前端开合角********************//

//输入合法性判断，物理极限是[0,30]度
endAngle:= LIMIT(minEndAng, endAngle, maxEndAng);

SIN_C:=AB*SIN(Angle_Initial_A)/BC;
Angle_Initial_C:=ASIN(SIN_C);

End_Angle_Single:=endAngle/2;
Angle_Inc_C:=End_Angle_Single;
Angle_C:=Angle_Initial_C+Angle_Inc_C;

SIN_A:=BC*SIN(Angle_C)/AB;
Angle_A:=ASIN(SIN_A);

Angle_Inc_A:=Angle_A-Angle_Initial_A;
frontAngle:=Angle_Inc_A*2;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="init" Id="{b6799ae1-8e52-4442-8aea-44a9f33a3afc}">
      <Declaration><![CDATA[METHOD PUBLIC init : BOOL
VAR_INPUT
	armIdx :INT;
END_VAR
VAR
	i:INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Update the IP and port number of the operating arm
m_armNetId:=GVL_SlaveJointEncoderCaliData.g_slaveArmNetID[armIdx];
m_jointInstruAddr:=GVL_SlaveJointEncoderCaliData.g_slaveJointNetPort[armIdx,4];

// init joint motion range
FOR i:=1 TO 3 DO
	m_kinematicParameters.m_wristMinDHJntPos[i]:=GVL_SlaveMotorParameters.g_minDHJntPos[i+3];
	m_kinematicParameters.m_wristMaxDHJntPos[i]:=GVL_SlaveMotorParameters.g_maxDHJntPos[i+3];
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="initInstruJntPos" Id="{9abce61f-f34d-42ca-a401-a8190ac28e6c}">
      <Declaration><![CDATA[METHOD PUBLIC initInstruJntPos : BOOL
VAR_INPUT
	m_curJntPos :Vec8d;
END_VAR

VAR
	i:INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
FOR i := 4 TO 8 BY 1 DO
	m_jntPosOffset[i]:=m_curJntPos[i];
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="instruFK" Id="{097c05ed-881a-44a5-815d-27300cef479f}">
      <Declaration><![CDATA[// convert motor joint position to DH joint position
METHOD PUBLIC instruFK : BOOL
VAR_IN_OUT CONSTANT
	// motor joint position of slave arm
	i_jntPos :Vec8d;
END_VAR

VAR_IN_OUT
	// instrument DH  joint positon
	r_DHJntPos :Vec6d;

	// gripper angle
	r_gripAngle :LREAL;
END_VAR

VAR
	jntPosL1,jntPosL2,jntPosL3,jntPosL4,jntPosL5 :LREAL;
	gripAngle:LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[r_DHJntPos[1]:=i_jntPos[1];
r_DHJntPos[2]:=i_jntPos[2];
r_DHJntPos[3]:=i_jntPos[3];

// ################## endoscope ############################
IF m_instruType = InstruType_endoscope THEN
	r_DHJntPos[4]:=m_endoscopeRotateReduction*(i_jntPos[8]-m_jntPosOffset[8]);
	IF m_curEndoscopeType = EndoScopeType_30 THEN
		r_DHJntPos[5]:=-pi/2+30*g_deg2Rad;
	ELSE
		r_DHJntPos[5]:=-pi/2;
	END_IF
	r_DHJntPos[6]:=0;
	RETURN;
END_IF

//#################### instrument forward kinematic ############
// convert motor joint position to instrument joint position
jntPosL1:=(i_jntPos[4]-m_jntPosOffset[4]);
jntPosL2:=(i_jntPos[5]-m_jntPosOffset[5]);
jntPosL3:=(i_jntPos[6]-m_jntPosOffset[6]);
jntPosL4:=-(i_jntPos[7]-m_jntPosOffset[7]);
jntPosL5:=-(i_jntPos[8]-m_jntPosOffset[8]);

// diable L1 if instrument is single finger
IF m_isSingleFinger THEN
	jntPosL1 := -jntPosL2;
END_IF

// instrument FK in local coordinate
r_DHJntPos[4]:=jntPosL4/m_kinematicParameters.kx;
r_DHJntPos[5]:=jntPosL3/m_kinematicParameters.ky;
r_DHJntPos[6]:=(jntPosL1-jntPosL2-2*r_DHJntPos[5]*m_kinematicParameters.k13*m_kinematicParameters.kz)/(2*m_kinematicParameters.m_openAngleDir*m_kinematicParameters.kz);

// convert instrument rotation to arm DH joint position
r_DHJntPos[5]:=-r_DHJntPos[5]-pi/2;
r_DHJntPos[6]:=-r_DHJntPos[6];

// grip angle
gripAngle:=(jntPosL1+jntPosL2)/(m_kinematicParameters.m_openAngleDir*m_kinematicParameters.kz);

//################### gripper angle handle #################
IF (InstruType_ProGraspForceps=m_instruType) THEN//细齿无创抓钳
	gripperAngleForwardTransform_ID12(frontAngle:= gripAngle, endAngle=> r_gripAngle);

ELSIF (InstruType_CauteryHook = m_instruType) THEN//单级电钩
	r_gripAngle:=0;
	
ELSE
	r_gripAngle:=gripAngle;
END_IF

]]></ST>
      </Implementation>
    </Method>
    <Method Name="InstruIDRecord" Id="{f3567262-d84c-4741-97fb-36ae0b451ee9}">
      <Declaration><![CDATA[METHOD PROTECTED InstruIDRecord :ARRAY [1..100] OF  ULINT
VAR_INPUT
	i_NewValue:ULINT;
	i_ArrayData:ARRAY [1..100] OF ULINT;
END_VAR

VAR
	k1:INT;
	Have_Data:BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
FOR k1:= 1 TO 100 BY 1 DO
	IF i_NewValue=i_ArrayData[k1] THEN
		Have_Data:=TRUE;
		EXIT;
	ELSE
		Have_Data:=FALSE;
	END_IF
END_FOR

IF Have_Data=FALSE THEN
	FOR k1:= 1 TO 99 BY 1 DO
		InstruIDRecord[k1+1]:=i_ArrayData[k1];
	END_FOR
	InstruIDRecord[1]:=i_NewValue;
ELSE
	InstruIDRecord:=i_ArrayData ;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="instruIK" Id="{9a18f93c-de08-4a8c-a2e3-32833c619719}">
      <Declaration><![CDATA[METHOD PUBLIC instruIK : BOOL
VAR_INPUT
	// DH  joint positon
	i_DHJntPos :Vec6d;

	// gripper angle
	i_gripAngle :LREAL;
	
END_VAR
VAR_OUTPUT
	o_jntPos:Vec8d;
END_VAR
VAR
	gripAngle:LREAL;
	i:INT;
	rot_x:lreal;
	rot_y:lreal;
	rot_z:lreal;

	kx,ky,kz,k13,k23:LREAL;
	open_direction:INT;
	
	d,ra:LREAL;
	AngleOffsetL3:LREAL;
	m_targetOffsetL3:LREAL;
	m_actOffsetL3:LREAL;
	
	m_gripAnglePbox:Vec8d;

	L3Offset:LREAL:=-3;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[o_jntPos[1] := i_DHJntPos[1];
o_jntPos[2] := i_DHJntPos[2];
o_jntPos[3] := i_DHJntPos[3];

// ################## endoscope ############################
IF m_instruType = InstruType_endoscope THEN
	o_jntPos[8] := -i_DHJntPos[4]/m_endoscopeRotateReduction + m_jntPosOffset[8];
	o_jntPos[4]:=0;
	o_jntPos[5]:=0;
	o_jntPos[6]:=0;
	o_jntPos[7]:=0;
	RETURN;
END_IF

//################### gripper angle handle #################
IF (12=onChipData.m_InstruID) THEN//细齿无创抓钳
	gripperAngleInverseTransform_ID12(endAngle:= i_gripAngle, frontAngle=> gripAngle);
	
ELSIF 16 = onChipData.m_InstruID THEN//单级电钩\电铲
	gripAngle:=0;
	
ELSE
	gripAngle:=i_gripAngle;
END_IF

// ################# instrument inverse kinematic ##############
// convert arm DH joint position to  instrument rotation
rot_x:=i_DHJntPos[4];
rot_y:=-i_DHJntPos[5]-pi/2;
rot_z:=-i_DHJntPos[6];

// instrument IK in local coordinate
k13:=m_kinematicParameters.k13;
k23:=m_kinematicParameters.k23;
kx:=m_kinematicParameters.kx;
ky:=m_kinematicParameters.ky;
kz:=m_kinematicParameters.kz;
d:=m_kinematicParameters.m_openAngleDir;

m_gripAnglePbox[7]:=rot_x*kx;
m_gripAnglePbox[6]:=rot_y*ky;
ra:=gripAngle;
m_gripAnglePbox[4]:=rot_y*k13*kz+d*rot_z*kz+d*ra*kz/2;
m_gripAnglePbox[5]:=-rot_y*k23*kz-d*rot_z*kz+d*ra*kz/2;

// convert instrument joint position to motor joint position
o_jntPos[4] := m_gripAnglePbox[4] + m_jntPosOffset[4];
o_jntPos[5] := m_gripAnglePbox[5] + m_jntPosOffset[5];
o_jntPos[6] := m_gripAnglePbox[6] + m_jntPosOffset[6];
o_jntPos[7] := -m_gripAnglePbox[7] + m_jntPosOffset[7];
o_jntPos[8] := -m_gripAnglePbox[8] + m_jntPosOffset[8];


]]></ST>
      </Implementation>
    </Method>
    <Method Name="instruJntVelFK" Id="{a9276aa2-9909-457e-91a6-03a2e37295b6}">
      <Declaration><![CDATA[// convert motor joint velocity to DH joint velocity
METHOD PUBLIC instruJntVelFK : BOOL
VAR_IN_OUT CONSTANT
	// motor joint velocity of slave arm
	i_jntVel :Vec8d;
END_VAR

VAR_IN_OUT
	// instrument DH  joint velocity
	r_DHJntVel :Vec6d;
END_VAR

VAR
	jntPosL1,jntPosL2,jntPosL3,jntPosL4,jntPosL5 :LREAL;
	gripAngle:LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[r_DHJntVel[1]:=i_jntVel[1];
r_DHJntVel[2]:=i_jntVel[2];
r_DHJntVel[3]:=i_jntVel[3];

r_DHJntVel[4]:=-i_jntVel[7]/m_kinematicParameters.kx;
r_DHJntVel[5]:=i_jntVel[6]/m_kinematicParameters.ky;
r_DHJntVel[6]:=(i_jntVel[4]-i_jntVel[5]-2*r_DHJntVel[5]*m_kinematicParameters.k13*m_kinematicParameters.kz)/(2*m_kinematicParameters.m_openAngleDir*m_kinematicParameters.kz);
r_DHJntVel[5]:=-r_DHJntVel[5];
r_DHJntVel[6]:=-r_DHJntVel[6];]]></ST>
      </Implementation>
    </Method>
    <Method Name="InstruSreiesFind" Id="{f880300d-e9dd-456f-88b6-72b1cc8bd3d5}">
      <Declaration><![CDATA[METHOD PROTECTED InstruSreiesFind : BOOL
VAR_INPUT
	i_NewValue:ULINT;
	i_ArrayData:ARRAY [1..100] OF ULINT;
	
END_VAR

VAR
	k1:INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[InstruSreiesFind:=FALSE;
FOR k1:= 1 TO 100 BY 1 DO
	IF i_NewValue=i_ArrayData[k1] THEN
		InstruSreiesFind:=TRUE;
		EXIT;
	ELSE
		InstruSreiesFind:=FALSE;
	END_IF
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Property Name="instruType" Id="{dee9b466-61c8-4830-8c3c-8317ba8d7fb1}" FolderPath="property\">
      <Declaration><![CDATA[PROPERTY PUBLIC instruType : INT]]></Declaration>
      <Get Name="Get" Id="{49e23c7c-bb55-4631-aecb-7cce8ba4d643}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[instruType := m_instruType;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="kinematicParameters" Id="{e7ad17bd-82e9-477e-9e66-6a8feac51534}" FolderPath="property\">
      <Declaration><![CDATA[PROPERTY kinematicParameters :REFERENCE TO ST_InstruKinematicParam]]></Declaration>
      <Get Name="Get" Id="{d0447347-931d-4e2e-81b2-c8e4acc99fd6}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[kinematicParameters ref=m_kinematicParameters;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="nextScopePosture" Id="{48ece37f-c5c3-453e-afe5-8c33fe7eb315}" FolderPath="property\">
      <Declaration><![CDATA[PROPERTY nextScopePosture : E_EndoscopePosture]]></Declaration>
      <Get Name="Get" Id="{07de26d8-dacc-432f-85c8-f04024d47afd}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[nextScopePosture:= m_nextEndoscopePosture;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{33462dd0-4b03-403d-a99e-110b0590fa7e}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[m_nextEndoscopePosture := nextScopePosture;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="nextScopeType" Id="{dca96b48-db31-4f08-81c0-25e9bd0712eb}" FolderPath="property\">
      <Declaration><![CDATA[PROPERTY nextScopeType : E_EndoscopePosture]]></Declaration>
      <Get Name="Get" Id="{815f56b6-cf18-45a1-a9a3-a7d0c73baa6e}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[nextScopeType:= m_nextEndoscopeType;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{a373dd0f-9a3d-404d-a5d3-2cea57ae861a}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[m_nextEndoscopeType := nextScopeType;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="onChipData" Id="{093a4217-bb32-41a8-a667-4570e97851b1}" FolderPath="property\">
      <Declaration><![CDATA[PROPERTY onChipData : REFERENCE TO ST_InstruOnChipData]]></Declaration>
      <Get Name="Get" Id="{f9328823-d656-4b2e-b51e-d2d3d6c6297d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[onChipData ref=m_onChipData;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="readParameters" Id="{fafb5a79-7caf-47d4-9d2f-b3dbd95048cf}">
      <Declaration><![CDATA[METHOD PUBLIC readParameters : BOOL
VAR_INPUT
	startFlag :BOOL;
END_VAR

VAR
	paramIdx :INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// TODO: read m_kinematic parameters from chip 
//Take the rising edge of the read signal
m_ReadDelay(IN:= m_Step=10, PT:= m_DealyTime, Q=> , ET=> );//计时器
IF startFlag AND m_curReadTries < m_maxReadTries THEN
	m_Step:=0;
	m_onChipData.m_InstruReadSucceed:=FALSE;
	m_onChipData.m_Error:=FALSE;
	m_onChipData.m_ErrorID:=0;
END_IF


CASE m_Step OF
	
	0://向6093发送0x8000  0000---发送读取信号
		m_Value:=16#80000000; 
		m_SdoWrite(sNetId:= m_ArmNetId, nSlaveAddr:= m_JointInstruAddr, nSubIndex:= 0, nIndex:= 16#6093, pSrcBuf:= ADR(m_Value), cbBufLen:= SIZEOF(m_Value), bExecute:= TRUE, );
		IF NOT m_SdoWrite.bBusy THEN
			m_SdoWrite(bExecute:=FALSE);
			m_Step:=10;
		END_IF
		
		
	10://延时0.5s、防止值还没有返回
		IF m_ReadDelay.Q THEN
			m_Step:=20;
		END_IF

		
	20://从6093地址中拿值
		m_SdoRead(sNetId:= m_ArmNetId, nSlaveAddr:= m_JointInstruAddr,	nSubIndex:= 0, nIndex:= 16#6093, pDstBuf:= ADR(m_IDReadResult), cbBufLen:= SIZEOF(m_IDReadResult), bExecute:= TRUE, 	tTimeout:= , bBusy=> , bError=> , nErrId=> , cbRead=> );
		IF NOT m_SdoRead.bBusy THEN
			m_SdoRead(bExecute:=FALSE);
			m_Step:=30;
		END_IF	
		
	
	30://处理器械ID、低16位
		m_IDResult:=UDINT_TO_ULINT(16#0000FFFF AND m_IDReadResult);
		m_Step:=40;
		
		
	40://读取6095、器械序列号
		m_SdoRead(sNetId:= m_ArmNetId, nSlaveAddr:= m_JointInstruAddr,	nSubIndex:= 0, nIndex:= 16#6095, pDstBuf:= ADR(m_SeriesReadResult), cbBufLen:= SIZEOF(m_SeriesReadResult), bExecute:= TRUE, 	tTimeout:= , bBusy=> , bError=> , nErrId=> , cbRead=> );
		IF NOT m_SdoRead.bBusy THEN
			m_SdoRead(bExecute:=FALSE);
			m_Step:=50;
		END_IF	
	
		
	50://处理序列号，低30位
		m_onChipData.m_InstruseriesNum:=SHL(m_IDResult,31)+(16#7FFFFFFF AND m_SeriesReadResult);
		m_onChipData.m_InstruID:=LREAL_TO_INT(FLOOR(lr_in:= m_onChipData.m_InstruseriesNum/EXPT(10,9))); //取十进制前三位为器械ID
		m_Step:=60;
		
		
	60://读取6096---器械零点位置
		m_SdoRead(sNetId:= m_ArmNetId, nSlaveAddr:= m_JointInstruAddr,	nSubIndex:= 0, nIndex:= 16#6096, pDstBuf:= ADR(m_PosReadResult), cbBufLen:= SIZEOF(m_PosReadResult), bExecute:= TRUE, 	tTimeout:= , bBusy=> , bError=> , nErrId=> , cbRead=> );
		IF NOT m_SdoRead.bBusy THEN
			m_SdoRead(bExecute:=FALSE);
			m_Step:=70;
		END_IF	
		
		
	70: //处理位置，L1:0-9位，L2:10-19位，L3:20-29位
		zeroPosDecode(m_PosReadResult, o_q1=>m_onChipData.m_InstruPos[1],o_q2=>m_onChipData.m_InstruPos[2],o_q3=>m_onChipData.m_InstruPos[3]);
		m_onChipData.m_InstruPos[4] := m_jnt4ZeroPos;
		m_onChipData.m_InstruPos[5] := 0;
		
		m_Step:=80;
		
	80://读取器械使用次数 6094
		m_SdoRead(sNetId:= m_ArmNetId, nSlaveAddr:= m_JointInstruAddr,	nSubIndex:= 0, nIndex:= 16#6094, pDstBuf:= ADR(m_UsageReadResult), cbBufLen:= SIZEOF(m_UsageReadResult), bExecute:= TRUE, 	tTimeout:= , bBusy=> , bError=> , nErrId=> , cbRead=> );
		IF NOT m_SdoRead.bBusy THEN
			m_SdoRead(bExecute:=FALSE);
			m_Step:=90;
		END_IF	
		
		
	90://处理器械使用次数读取结果
		m_onChipData.m_InstruusageCounts:=m_UsageReadResult;
		m_InstruUsageToLED:=LIMIT(0,m_UsageReadResult,99);
		m_Step:=110;
		
		
	100://将器械使用次数写入LED
		m_Value:=ULINT_TO_UDINT(16#FFFF AND m_InstruUsageToLED) ;
		m_SdoWrite(sNetId:= m_ArmNetId, nSlaveAddr:= m_LEDPortAddr, nSubIndex:= 06, nIndex:= 16#6090, pSrcBuf:= ADR(m_Value), cbBufLen:= SIZEOF(m_Value), bExecute:= TRUE, tTimeout:= , bBusy=> , bError=> , nErrId=> );
		IF NOT m_SdoWrite.bBusy THEN
			m_SdoWrite(bExecute:=FALSE);
			m_Step:=110;
		END_IF
		
	110://处理当器械使用次数达到上限后、在同一场手术中还能使用
		IF m_onChipData.m_InstruusageCounts=m_onChipData.m_InstrumaxUsageCounts THEN
			m_InstruSeriesRecord:=InstruIDRecord(i_NewValue:= m_onChipData.m_InstruusageCounts, i_ArrayData:= m_InstruSeriesRecord);
			m_Step:=120;
		ELSE
			m_Step:=120;
		END_IF
		
		
	120://结果诊断
		m_onChipData.m_InstruReadSucceed:=checkInstruType();
		IF NOT(m_onChipData.m_InstruReadSucceed) THEN
			m_onChipData.m_Error:=TRUE;
			m_onChipData.m_ErrorID:=1;
(*		ELSIF m_onChipData.m_InstruusageCounts>m_onChipData.m_InstrumaxUsageCounts AND m_onChipData.m_InstruID<>20 THEN
			m_HaveRecord:=InstruSreiesFind(i_NewValue:=m_onChipData.m_InstruseriesNum , i_ArrayData:=m_InstruSeriesRecord );
			IF m_HaveRecord THEN
				m_onChipData.m_Instrutype:=2;
				m_onChipData.m_InstruReadSucceed:=TRUE;
				m_Step:=130;
			ELSE
				m_onChipData.m_Error:=TRUE;
				m_onChipData.m_ErrorID:=2;
			END_IF*)
		END_IF
		m_Step :=-1;
		m_curReadTries := m_curReadTries +1;
	
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="reset" Id="{90292eaa-1cc4-4b19-b4f6-6bcd7c03eb33}">
      <Declaration><![CDATA[METHOD PUBLIC reset : BOOL
VAR_INPUT
END_VAR
VAR
	i:INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[m_Step := -1;
m_curReadTries := 0;
m_onChipData.m_InstruID := 0;
m_onChipData.m_InstruseriesNum :=0;
m_onChipData.m_Instrutype :=InstruType_UNKNOWM;
m_onChipData.m_Error:=FALSE;
m_onChipData.m_ErrorID:=0;
m_onChipData.m_InstruReadSucceed:=FALSE;
m_isSingleFinger:=FALSE;
m_instruType:=InstruType_UNKNOWM;

FOR i:= 4 TO 8 BY 1 DO
	m_jntPosOffset[i]:=0;
END_FOR

// init joint motion range
FOR i:=1 TO 3 DO
	m_kinematicParameters.m_wristMinDHJntPos[i]:=GVL_SlaveMotorParameters.g_minDHJntPos[i+3];
	m_kinematicParameters.m_wristMaxDHJntPos[i]:=GVL_SlaveMotorParameters.g_maxDHJntPos[i+3];
END_FOR

// reset endoscope type and posture
m_curEndoscopeType :=EndoScopeType_30;
m_nextEndoscopeType :=EndoScopeType_30;
m_curEndoscopePosture :=EndoscopePosture_HeadDown;
m_nextEndoscopePosture :=EndoscopePosture_HeadDown;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="writeZeroPosToChip" Id="{bd19b6c1-405e-4351-971f-f03bf3bea173}">
      <Declaration><![CDATA[METHOD PUBLIC writeZeroPosToChip : BOOL
VAR_INPUT
	i_q1:LREAL;
	i_q2:LREAL;
	i_q3:LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[zeroPosCode(i_q1,i_q2,i_q3, o_code=>m_zeroPosCode);

writeZeroPosToChip:=FALSE;
m_SdoWrite(sNetId:= m_ArmNetId, nSlaveAddr:= m_JointInstruAddr, nSubIndex:= 0, nIndex:= 16#6096, pSrcBuf:= ADR(m_zeroPosCode), cbBufLen:= SIZEOF(m_zeroPosCode), bExecute:= TRUE, );
IF NOT m_SdoWrite.bBusy THEN
	m_SdoWrite(bExecute:=FALSE);
	writeZeroPosToChip:=TRUE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="zeroPosCode" Id="{84c93418-be66-4d42-a7c9-9496164ac08f}">
      <Declaration><![CDATA[// code instrument joints(4/5/6) zero position as a UDINT
// joint1 : bit 21-30; joint2: bit 11-20; joint3: bit1-10
// zeroPos: [0, 2pi]; code:[0-3sFF]
METHOD PROTECTED zeroPosCode : BOOL
VAR_INPUT
	i_q1:LREAL;
	i_q2:LREAL;
	i_q3:LREAL;
END_VAR
VAR_OUTPUT
	o_code:UDINT;
END_VAR
VAR
	posCode:UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// highest bit set to 1
o_code:=2;

// joint1
posCode := LREAL_TO_UDINT(16#3FF * LIMIT(0,i_q1/g_2pi,1));
o_code := SHL(o_code, 10);
o_code := o_code OR posCode;

// joint2
posCode := LREAL_TO_UDINT(16#3FF * LIMIT(0,i_q2/g_2pi,1));
o_code := SHL(o_code, 10);
o_code := o_code OR posCode;

// joint3
posCode := LREAL_TO_UDINT(16#3FF * LIMIT(0,i_q3/g_2pi,1));
o_code := SHL(o_code, 10);
o_code := o_code OR posCode;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="zeroPosDecode" Id="{7d5604d0-0288-46b2-b433-10a7497ca58f}">
      <Declaration><![CDATA[// decode instrument joints(4/5/6) zero position from a UDINT
// joint1 : bit 21-30; joint2: bit 11-20; joint4: bit1-10
// zeroPos: [0, 2pi]; code:[0-2FF]
METHOD PROTECTED zeroPosDecode : BOOL
VAR_INPUT
	i_posCode :UDINT;
END_VAR
VAR_OUTPUT
	o_q1:LREAL;
	o_q2:LREAL;
	o_q3:LREAL;
END_VAR
VAR
	posCode:UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// joint3
posCode := i_posCode AND 16#3FF;
o_q3 := g_2pi *  posCode/16#3FF;

// joint2
i_posCode := SHR(i_posCode, 10);
posCode := i_posCode AND 16#3FF;
o_q2 := g_2pi *  posCode/16#3FF;

// joint1
i_posCode := SHR(i_posCode, 10);
posCode := i_posCode AND 16#3FF;
o_q1 := g_2pi *  posCode/16#3FF;
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_Instrument">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.checkInstruType">
      <LineId Id="10" Count="0" />
      <LineId Id="115" Count="0" />
      <LineId Id="102" Count="0" />
      <LineId Id="101" Count="0" />
      <LineId Id="113" Count="1" />
      <LineId Id="123" Count="0" />
      <LineId Id="130" Count="1" />
      <LineId Id="38" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="116" Count="0" />
      <LineId Id="104" Count="4" />
      <LineId Id="103" Count="0" />
      <LineId Id="109" Count="0" />
      <LineId Id="52" Count="5" />
      <LineId Id="64" Count="5" />
      <LineId Id="63" Count="0" />
      <LineId Id="71" Count="5" />
      <LineId Id="70" Count="0" />
      <LineId Id="88" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="89" Count="0" />
      <LineId Id="92" Count="0" />
      <LineId Id="137" Count="0" />
      <LineId Id="133" Count="0" />
      <LineId Id="135" Count="1" />
      <LineId Id="91" Count="0" />
      <LineId Id="51" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="18" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.curScopePosture.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.curScopePosture.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.curScopeType.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.curScopeType.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.gripAngleMap">
      <LineId Id="80" Count="0" />
      <LineId Id="83" Count="3" />
      <LineId Id="82" Count="0" />
      <LineId Id="81" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="46" Count="0" />
      <LineId Id="26" Count="3" />
      <LineId Id="24" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.gripperAngleForwardTransform_ID12">
      <LineId Id="6" Count="3" />
      <LineId Id="44" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="20" Count="12" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.gripperAngleInverseTransform_ID12">
      <LineId Id="12" Count="3" />
      <LineId Id="54" Count="0" />
      <LineId Id="24" Count="13" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.init">
      <LineId Id="7" Count="1" />
      <LineId Id="5" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="13" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.initInstruJntPos">
      <LineId Id="9" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="11" Count="1" />
    </LineIds>
    <LineIds Name="FB_Instrument.instruFK">
      <LineId Id="131" Count="0" />
      <LineId Id="134" Count="1" />
      <LineId Id="189" Count="0" />
      <LineId Id="188" Count="0" />
      <LineId Id="190" Count="0" />
      <LineId Id="193" Count="0" />
      <LineId Id="196" Count="1" />
      <LineId Id="199" Count="1" />
      <LineId Id="198" Count="0" />
      <LineId Id="191" Count="0" />
      <LineId Id="195" Count="0" />
      <LineId Id="192" Count="0" />
      <LineId Id="201" Count="0" />
      <LineId Id="132" Count="1" />
      <LineId Id="126" Count="4" />
      <LineId Id="222" Count="0" />
      <LineId Id="221" Count="0" />
      <LineId Id="223" Count="2" />
      <LineId Id="160" Count="0" />
      <LineId Id="156" Count="0" />
      <LineId Id="86" Count="1" />
      <LineId Id="89" Count="0" />
      <LineId Id="181" Count="2" />
      <LineId Id="136" Count="0" />
      <LineId Id="185" Count="0" />
      <LineId Id="184" Count="0" />
      <LineId Id="90" Count="0" />
      <LineId Id="187" Count="0" />
      <LineId Id="186" Count="0" />
      <LineId Id="91" Count="8" />
      <LineId Id="158" Count="1" />
    </LineIds>
    <LineIds Name="FB_Instrument.InstruIDRecord">
      <LineId Id="12" Count="16" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.instruIK">
      <LineId Id="528" Count="1" />
      <LineId Id="527" Count="0" />
      <LineId Id="532" Count="2" />
      <LineId Id="543" Count="0" />
      <LineId Id="541" Count="0" />
      <LineId Id="544" Count="2" />
      <LineId Id="542" Count="0" />
      <LineId Id="531" Count="0" />
      <LineId Id="547" Count="0" />
      <LineId Id="530" Count="0" />
      <LineId Id="549" Count="9" />
      <LineId Id="548" Count="0" />
      <LineId Id="480" Count="0" />
      <LineId Id="395" Count="2" />
      <LineId Id="481" Count="0" />
      <LineId Id="390" Count="0" />
      <LineId Id="389" Count="0" />
      <LineId Id="391" Count="0" />
      <LineId Id="388" Count="0" />
      <LineId Id="392" Count="2" />
      <LineId Id="7" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="385" Count="0" />
      <LineId Id="50" Count="1" />
      <LineId Id="56" Count="0" />
      <LineId Id="483" Count="0" />
      <LineId Id="492" Count="4" />
      <LineId Id="324" Count="1" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.instruJntVelFK">
      <LineId Id="131" Count="0" />
      <LineId Id="134" Count="1" />
      <LineId Id="200" Count="3" />
      <LineId Id="159" Count="0" />
      <LineId Id="220" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.InstruSreiesFind">
      <LineId Id="34" Count="8" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.instruType.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.kinematicParameters.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.nextScopePosture.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.nextScopePosture.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.nextScopeType.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.nextScopeType.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.onChipData.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.readParameters">
      <LineId Id="5" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="219" Count="0" />
      <LineId Id="222" Count="2" />
      <LineId Id="220" Count="1" />
      <LineId Id="22" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="43" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="46" Count="1" />
      <LineId Id="45" Count="0" />
      <LineId Id="51" Count="2" />
      <LineId Id="29" Count="0" />
      <LineId Id="57" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="61" Count="2" />
      <LineId Id="58" Count="0" />
      <LineId Id="54" Count="0" />
      <LineId Id="64" Count="2" />
      <LineId Id="70" Count="1" />
      <LineId Id="74" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="76" Count="3" />
      <LineId Id="75" Count="0" />
      <LineId Id="73" Count="0" />
      <LineId Id="80" Count="1" />
      <LineId Id="83" Count="0" />
      <LineId Id="82" Count="0" />
      <LineId Id="86" Count="0" />
      <LineId Id="55" Count="0" />
      <LineId Id="90" Count="1" />
      <LineId Id="93" Count="3" />
      <LineId Id="92" Count="0" />
      <LineId Id="97" Count="0" />
      <LineId Id="131" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="395" Count="2" />
      <LineId Id="361" Count="0" />
      <LineId Id="104" Count="0" />
      <LineId Id="142" Count="0" />
      <LineId Id="56" Count="0" />
      <LineId Id="137" Count="3" />
      <LineId Id="136" Count="0" />
      <LineId Id="135" Count="0" />
      <LineId Id="150" Count="0" />
      <LineId Id="133" Count="0" />
      <LineId Id="145" Count="0" />
      <LineId Id="147" Count="0" />
      <LineId Id="134" Count="0" />
      <LineId Id="148" Count="1" />
      <LineId Id="132" Count="0" />
      <LineId Id="155" Count="4" />
      <LineId Id="154" Count="0" />
      <LineId Id="151" Count="1" />
      <LineId Id="162" Count="0" />
      <LineId Id="167" Count="0" />
      <LineId Id="164" Count="2" />
      <LineId Id="161" Count="0" />
      <LineId Id="273" Count="1" />
      <LineId Id="170" Count="0" />
      <LineId Id="387" Count="0" />
      <LineId Id="172" Count="0" />
      <LineId Id="174" Count="0" />
      <LineId Id="176" Count="1" />
      <LineId Id="180" Count="1" />
      <LineId Id="249" Count="0" />
      <LineId Id="182" Count="0" />
      <LineId Id="281" Count="0" />
      <LineId Id="184" Count="2" />
      <LineId Id="183" Count="0" />
      <LineId Id="175" Count="0" />
      <LineId Id="405" Count="1" />
      <LineId Id="276" Count="0" />
      <LineId Id="28" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.reset">
      <LineId Id="51" Count="1" />
      <LineId Id="5" Count="2" />
      <LineId Id="11" Count="1" />
      <LineId Id="16" Count="0" />
      <LineId Id="58" Count="0" />
      <LineId Id="79" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="22" Count="1" />
      <LineId Id="21" Count="0" />
      <LineId Id="41" Count="4" />
      <LineId Id="39" Count="0" />
      <LineId Id="73" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="68" Count="1" />
      <LineId Id="72" Count="0" />
      <LineId Id="65" Count="0" />
      <LineId Id="17" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.writeZeroPosToChip">
      <LineId Id="16" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="17" Count="4" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.zeroPosCode">
      <LineId Id="39" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="40" Count="0" />
      <LineId Id="45" Count="2" />
      <LineId Id="58" Count="0" />
      <LineId Id="51" Count="2" />
      <LineId Id="50" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="55" Count="2" />
      <LineId Id="54" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Instrument.zeroPosDecode">
      <LineId Id="40" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="58" Count="0" />
      <LineId Id="51" Count="0" />
      <LineId Id="64" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="65" Count="0" />
      <LineId Id="68" Count="3" />
      <LineId Id="67" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>