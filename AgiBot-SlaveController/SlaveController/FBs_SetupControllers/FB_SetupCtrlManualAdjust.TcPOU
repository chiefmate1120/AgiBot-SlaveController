<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_SetupCtrlManualAdjust" Id="{a66618bf-53b6-41ea-9ead-5a7cf0054c7f}" SpecialFunc="None">
    <Declaration><![CDATA[// Brief: Setup Arm Adjust
FUNCTION_BLOCK PUBLIC FB_SetupCtrlManualAdjust EXTENDS FB_SetupArmCtrlBase
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	// friction compensation scale
	m_frictionScale :LREAL :=0.5;
	
	// velocity threshold for friction compensation 
	m_jnt3FrictionCompVelLow :LREAL := 0.0005;
	m_frictionScaleLow :LREAL :=0.3;
	m_jnt3FrictionCompVelMiddle :LREAL := 0.005;
	m_frictionScaleMiddle :LREAL :=0.5;
	m_jnt3FrictionCompVelHigh :LREAL := 0.05;
	m_frictionScaleHigh :LREAL :=0.7;
	
	// jnt5 adjust velocity
	m_jnt5MaxAdjustVel :LREAL:=5.0*g_deg2Rad;
	m_jnt5MaxAdjustAcc :LREAL:=60*g_deg2Rad;
	
	// jnt5 enabled time period
	m_jnt5EnableTime :LREAL :=0;
	
	// time threshold to drive jnt5 after it's enabled
	m_jnt5AdjustWaitTime :LREAL := 0.25;

END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="calcCmdJntPos" Id="{a3473076-af2e-4c76-847c-85354c287698}">
      <Declaration><![CDATA[// override this function in each exact controller
METHOD PROTECTED calcCmdJntPos : BOOL
VAR_IN_OUT CONSTANT
	i_setupArm	:FB_SetupArm;
END_VAR


VAR 
	i :INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// enable/disenable joint5
IF i_setupArm.jnt5AdjustDir = 0 THEN
	m_jntEnableFlag[5] := 0;
	m_cmdJntPos[5]:=i_setupArm.curJntPos[5];
	m_jnt5EnableTime:=0;
	m_cmdJntVel[5]:=0;
	RETURN;
ELSE
	m_jntEnableFlag[5] := 1;
	m_jnt5EnableTime := m_jnt5EnableTime + g_setupArmCtrlCycleTime;
END_IF

// update command position
IF m_jnt5EnableTime>m_jnt5AdjustWaitTime THEN
	m_cmdJntVel[5]:=m_cmdJntVel[5] + i_setupArm.jnt5AdjustDir * m_jnt5MaxAdjustAcc * g_setupArmCtrlCycleTime;
	m_cmdJntVel[5]:=LIMIT(-m_jnt5MaxAdjustVel, m_cmdJntVel[5], m_jnt5MaxAdjustVel);
	
	// joint limit
	IF (m_cmdJntPos[5] > GVL_SetupMotorParameters.g_maxJntPos[i_setupArm.armIdx, 5] AND m_cmdJntVel[5]>0) OR (m_cmdJntPos[5] < GVL_SetupMotorParameters.g_minJntPos[i_setupArm.armIdx, 5] AND m_cmdJntVel[5]<0)  THEN
		m_cmdJntVel[5] := 0;
	END_IF
	
	m_cmdJntPos[5]:= m_cmdJntPos[5] + m_cmdJntVel[5] * g_setupArmCtrlCycleTime;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="calcCmdJntTrq" Id="{c618bd90-4444-4b54-b7cd-0646bf55f729}">
      <Declaration><![CDATA[// override this function in each exact controller
METHOD PROTECTED calcCmdJntTrq : BOOL
VAR_IN_OUT CONSTANT
	i_setupArm	:FB_SetupArm;
END_VAR
VAR
	i:INT;
	vel :LREAL;
	scale:LREAL;
	ratio:LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// set joint torque to zero in default
m_cmdJntTrq := g_zeroVec5d;

//enable/diable  joint1-4
IF NOT(i_setupArm.manualAdjustOn) THEN
	FOR i:=1 TO 4 DO
		m_jntEnableFlag[i] := 0;
		m_jntOPMode[i]:=DriverOPMode_Pos;
	END_FOR
	RETURN;
ELSE
	FOR i:=1 TO 4 DO
		m_jntEnableFlag[i] := 1;
		m_jntOPMode[i]:=DriverOPMode_Trq;
		m_cmdJntPos[i] := i_setupArm.curJntPos[i];
	END_FOR
END_IF

// friction scale
vel:=ABS(i_setupArm.curJntVel[3]);
IF vel < m_jnt3FrictionCompVelLow THEN
	m_frictionScale := m_frictionScaleLow * vel/m_jnt3FrictionCompVelLow ;
ELSIF  vel > m_jnt3FrictionCompVelLow AND vel  < m_jnt3FrictionCompVelMiddle THEN
	ratio:= (vel-m_jnt3FrictionCompVelLow)/(m_jnt3FrictionCompVelMiddle-m_jnt3FrictionCompVelLow);
	m_frictionScale :=m_frictionScaleLow+ratio * (m_frictionScaleMiddle-m_frictionScaleLow);
ELSIF  vel > m_jnt3FrictionCompVelMiddle AND vel  < m_jnt3FrictionCompVelHigh THEN
	ratio:= (vel-m_jnt3FrictionCompVelMiddle)/(m_jnt3FrictionCompVelHigh-m_jnt3FrictionCompVelMiddle);
	m_frictionScale :=m_frictionScaleMiddle+ratio * (m_frictionScaleHigh-m_frictionScaleMiddle);
ELSIF  vel > m_jnt3FrictionCompVelHigh THEN
	m_frictionScale := m_frictionScaleHigh;
END_IF

// add friction compensation
m_frictionScale := LIMIT(0, m_frictionScale, 1);
m_cmdJntTrq[3]:= m_frictionScale * sign(i_setupArm.curJntVel[3])*jnt3Friction(i_setupArm.armIdx, i_setupArm.curJntPos[3]);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="init" Id="{f9e2fdef-c3dd-4598-8bef-00a8d9aa9850}">
      <Declaration><![CDATA[METHOD PUBLIC init : BOOL
VAR_IN_OUT CONSTANT
	i_setupArm	:FB_SetupArm;
END_VAR
VAR_IN_OUT 
	r_setupArmCtrlCmd :ST_SetupArmCtrlCmds;
END_VAR

]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Init(i_setupArm,r_setupArmCtrlCmd);
m_jnt5EnableTime :=0;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="jnt3Friction" Id="{e5cc270a-8755-465e-9228-86a9a255b090}">
      <Declaration><![CDATA[METHOD PROTECTED jnt3Friction : Lreal
VAR_INPUT
	i_armIdx:INT;
	i_jnt3Pos :LREAL;
END_VAR

VAR
	q:LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[q := LIMIT(g_setupJnt3GravityPosRange[i_armIdx,1], i_jnt3Pos, g_setupJnt3GravityPosRange[i_armIdx,2]);
jnt3Friction:=g_setupJnt3FrictionParams[i_armIdx,1]*EXPT(q,3)+g_setupJnt3FrictionParams[i_armIdx,2]*EXPT(q,2)+
				g_setupJnt3FrictionParams[i_armIdx,3]*q+g_setupJnt3FrictionParams[i_armIdx,4];]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_SetupCtrlManualAdjust">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_SetupCtrlManualAdjust.calcCmdJntPos">
      <LineId Id="132" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="112" Count="1" />
      <LineId Id="126" Count="0" />
      <LineId Id="133" Count="0" />
      <LineId Id="114" Count="0" />
      <LineId Id="110" Count="1" />
      <LineId Id="127" Count="0" />
      <LineId Id="109" Count="0" />
      <LineId Id="131" Count="0" />
      <LineId Id="130" Count="0" />
      <LineId Id="116" Count="0" />
      <LineId Id="134" Count="1" />
      <LineId Id="145" Count="5" />
      <LineId Id="128" Count="1" />
      <LineId Id="115" Count="0" />
    </LineIds>
    <LineIds Name="FB_SetupCtrlManualAdjust.calcCmdJntTrq">
      <LineId Id="87" Count="0" />
      <LineId Id="86" Count="0" />
      <LineId Id="88" Count="0" />
      <LineId Id="56" Count="0" />
      <LineId Id="80" Count="0" />
      <LineId Id="90" Count="2" />
      <LineId Id="89" Count="0" />
      <LineId Id="67" Count="0" />
      <LineId Id="81" Count="0" />
      <LineId Id="93" Count="3" />
      <LineId Id="84" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="104" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="107" Count="0" />
      <LineId Id="118" Count="1" />
      <LineId Id="105" Count="0" />
      <LineId Id="112" Count="0" />
      <LineId Id="108" Count="0" />
      <LineId Id="114" Count="1" />
      <LineId Id="113" Count="0" />
      <LineId Id="116" Count="1" />
      <LineId Id="109" Count="0" />
      <LineId Id="134" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="135" Count="0" />
      <LineId Id="122" Count="1" />
    </LineIds>
    <LineIds Name="FB_SetupCtrlManualAdjust.init">
      <LineId Id="57" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="12" Count="0" />
    </LineIds>
    <LineIds Name="FB_SetupCtrlManualAdjust.jnt3Friction">
      <LineId Id="6" Count="1" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>