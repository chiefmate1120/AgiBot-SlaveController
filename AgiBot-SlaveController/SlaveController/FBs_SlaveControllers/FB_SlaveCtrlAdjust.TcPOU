<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_SlaveCtrlAdjust" Id="{065dc40e-4d51-49bc-8f28-94a80cbbd0fd}" SpecialFunc="None">
    <Declaration><![CDATA[// Brief: Slave Arm Adjust
FUNCTION_BLOCK PUBLIC FB_SlaveCtrlAdjust EXTENDS FB_SlaveArmCtrlBase
VAR
	// velocity for full damping
	m_maxAdjustVel :Vec3d := [1.2, 1.2, 0.3];
	
	// velocity start to enable damping
	m_velLBForDamp :Vec3d := [1.0, 1.0, 0.2];
	
	// max damp
	m_maxDamp :Vec3d := [50, 50, 500];
	
	// distance buffer from joint limit to start damp
	m_limitBufferForDamp :Vec3d :=[20*g_deg2Rad, 20*g_deg2Rad, 0.03];
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="calcCmdJntPos" Id="{702c20a9-7bcc-49ed-9f8d-b2e61f393510}">
      <Declaration><![CDATA[// override this function in each exact controller
METHOD PROTECTED calcCmdJntPos : BOOL
VAR_IN_OUT CONSTANT
	i_endoscopePose : ST_Frame;
	i_masterStatus :ST_MasterStatus;
	i_setupStatus: ST_SetupStatus;
	i_slaveArm :FB_SlaveArm;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[m_cmdJntPos[1]:=i_slaveArm.curJntPos[1];
m_cmdJntPos[2]:=i_slaveArm.curJntPos[2];
m_cmdJntPos[3]:=i_slaveArm.curJntPos[3];
IF m_jntOPMode[8] = DriverOPMode_Trq THEN
	m_cmdJntPos[8]:=i_slaveArm.curJntPos[8];
END_IF
IF m_jntOPMode[7] = DriverOPMode_Trq THEN
	m_cmdJntPos[7]:=i_slaveArm.curJntPos[7];
END_IF

m_cmdCartPose:=i_slaveArm.curCartPose;]]></ST>
      </Implementation>
    </Method>
    <Method Name="calcCmdJntTrq" Id="{fcf0acdf-cfca-4de6-b628-f0f18f7171ce}">
      <Declaration><![CDATA[// override this function in each exact controller
METHOD PROTECTED calcCmdJntTrq : BOOL
VAR_IN_OUT CONSTANT
	i_endoscopePose : ST_Frame;
	i_masterStatus :ST_MasterStatus;
	i_setupStatus: ST_SetupStatus;
	i_slaveArm :FB_SlaveArm;
END_VAR
VAR
	i :INT;
	dampScale :LREAL;
	limitDis:LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// set joint torque to zero in default
m_cmdJntTrq := g_zeroVec8d;

// enable endoscope rotation joint if external torque exceed specific value
IF i_slaveArm.instrument.onChipData.m_InstruReadSucceed AND i_slaveArm.instrument.instruType = InstruType_Endoscope AND ABS(i_slaveArm.filtJntCurrent[8]) > 100 THEN
		m_jntOPMode[8]:=DriverOPMode_Trq;
END_IF


// add damp near joint limit
FOR i:=1 TO 3 DO
	IF (i_slaveArm.curJntPos[i] > GVL_SlaveMotorParameters.g_maxDHJntPos[i] - m_limitBufferForDamp[i] AND  i_slaveArm.curJntVel[i]> 0 ) 
		OR (i_slaveArm.curJntPos[i] < GVL_SlaveMotorParameters.g_minDHJntPos[i] + m_limitBufferForDamp[i] AND  i_slaveArm.curJntVel[i]<0 )  THEN
		limitDis := MIN(GVL_SlaveMotorParameters.g_maxDHJntPos[i] - i_slaveArm.curJntPos[i], i_slaveArm.curJntPos[i]-GVL_SlaveMotorParameters.g_minDHJntPos[i]);
		dampScale  :=LIMIT(0, 1-limitDis/m_limitBufferForDamp[i],1);
		m_cmdJntTrq[i]:= -dampScale * m_maxDamp[i] * i_slaveArm.curJntVel[i];
	END_IF
	
END_FOR

// compute damp
FOR i:=1 TO 3 DO
	IF ABS(i_slaveArm.curJntVel[i]) > m_velLBForDamp[i] THEN 
		dampScale:=LIMIT(0, (ABS(i_slaveArm.curJntVel[i])- m_velLBForDamp[i])/(m_maxAdjustVel[i] - m_velLBForDamp[i]), 1 );
		m_cmdJntTrq[i] :=m_cmdJntTrq[i] -i_slaveArm.curJntVel[i] * m_maxDamp[i] * dampScale;
	END_IF
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="init" Id="{0b0d07b9-f0c8-4405-b76e-2338c9ecb922}">
      <Declaration><![CDATA[METHOD PUBLIC init : BOOL
VAR_IN_OUT CONSTANT
	i_slaveArm	:FB_SlaveArm;
END_VAR
VAR_IN_OUT 
	r_slaveArmCtrlCmd :ST_SlaveArmCtrlCmds;
END_VAR
VAR
	i :INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// check if to hold the command joint position unchanged
m_holdLastCmds := checkToHoldLastCmds(i_slaveArm);

//init
SUPER^.Init(i_slaveArm,r_slaveArmCtrlCmd);

// disable joints if this joint is an unrecoverable failure
FOR i:=1 TO g_sJntNum DO
	IF SafetyCheck.m_SafetyCheck.m_jntErrFlag[i_slaveArm.armIdx, 0, 3] OR  SafetyCheck.m_SafetyCheck.m_jntErrFlag[i_slaveArm.armIdx, i+g_setupJntNum, 3] THEN
		m_jntEnableFlag[i] := 0;
	ELSE
		m_jntEnableFlag[i] := 1;
	END_IF
END_FOR

// joint 1/2/3/7 run in torque mode
m_jntOPMode[1]:=DriverOPMode_Trq;
m_jntOPMode[2]:=DriverOPMode_Trq;
m_jntOPMode[3]:=DriverOPMode_Trq;
m_jntOPMode[4]:=DriverOPMode_PosTrq;
m_jntOPMode[5]:=DriverOPMode_PosTrq;
m_jntOPMode[6]:=DriverOPMode_PosTrq;
m_jntOPMode[7]:=DriverOPMode_PosTrq;
m_jntOPMode[8]:=DriverOPMode_PosTrq;

IF i_slaveArm.instrument.onChipData.m_InstruReadSucceed THEN
	IF i_slaveArm.instrument.instruType <> InstruType_Endoscope THEN
		m_jntOPMode[7]:=DriverOPMode_Trq;
	END_IF
END_IF

// update commands to arm
copyCmds(r_slaveArmCtrlCmd);
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_SlaveCtrlAdjust">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_SlaveCtrlAdjust.calcCmdJntPos">
      <LineId Id="27" Count="0" />
      <LineId Id="57" Count="1" />
      <LineId Id="76" Count="2" />
      <LineId Id="80" Count="1" />
      <LineId Id="79" Count="0" />
      <LineId Id="67" Count="0" />
      <LineId Id="39" Count="0" />
    </LineIds>
    <LineIds Name="FB_SlaveCtrlAdjust.calcCmdJntTrq">
      <LineId Id="5" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="155" Count="5" />
      <LineId Id="119" Count="0" />
      <LineId Id="118" Count="0" />
      <LineId Id="120" Count="0" />
      <LineId Id="148" Count="1" />
      <LineId Id="151" Count="0" />
      <LineId Id="153" Count="0" />
      <LineId Id="152" Count="0" />
      <LineId Id="150" Count="0" />
      <LineId Id="125" Count="1" />
      <LineId Id="105" Count="0" />
      <LineId Id="62" Count="0" />
      <LineId Id="68" Count="0" />
      <LineId Id="73" Count="1" />
      <LineId Id="76" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="71" Count="0" />
      <LineId Id="53" Count="0" />
    </LineIds>
    <LineIds Name="FB_SlaveCtrlAdjust.init">
      <LineId Id="157" Count="32" />
      <LineId Id="12" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>